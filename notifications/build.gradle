apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

def fcmCompileVersion = "20.3.0"

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 16
        //noinspection OldTargetApi
        targetSdkVersion 29
        versionName version
        buildConfigField "String", "LIB_VERSION", "\"$version\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'proguard-rules.pro'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    lintOptions {
        warningsAsErrors true
        lintConfig file('../lint.xml')
    }
    kotlinOptions.allWarningsAsErrors = true

    libraryVariants.all { variant ->
        variant.getRuntimeConfiguration().resolutionStrategy { strategy ->
            strategy.eachDependency { details ->
                if (details.requested.group == 'com.google.firebase'
                        && details.requested.name == 'firebase-messaging') {
                    // To skip compile-time dependency resolution checks
                    details.useVersion fcmCompileVersion
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // for runtime dependencies
    runtimeOnly "com.google.firebase:firebase-messaging:17.6.0"
    // for compile dependencies
    compileOnly "com.google.firebase:firebase-messaging:$fcmCompileVersion"
    implementation project(path: ':core')
    testImplementation "com.google.firebase:firebase-messaging:$fcmCompileVersion"
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.jetbrains.kotlin:kotlin-test'
    testImplementation 'androidx.test:core:1.3.0'
    testImplementation "com.google.truth:truth:1.0.1"
    testImplementation "io.mockk:mockk:1.10.0"
    testImplementation "org.robolectric:robolectric:4.8"
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.8.0'
    testImplementation project(path: ':core', configuration: 'testRuntime')
}
