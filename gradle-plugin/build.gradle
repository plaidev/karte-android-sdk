buildscript {
    ext.kotlin_version = '1.3.21'
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
    }
}

apply plugin: 'kotlin'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

group 'io.karte.android'
def moduleName = 'core'
def version_name = project.file('version').getText('UTF-8')
def gitUrl = 'https://github.com/plaidev/karte-android-sdk'

repositories {
    mavenCentral()
    jcenter()
    google()
}

// Include the generated Version file
sourceSets {
    main {
        java {
            srcDirs += ['src/main/kotlin']
        }
    }
}

dependencies {
    implementation gradleApi()
    compileOnly 'com.android.tools.build:gradle:3.5.0'
    implementation 'org.javassist:javassist:3.24.1-GA'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

task sourceJar(type: Jar) {
    classifier = "sources"
    from sourceSets.main.java.srcDirs
}

def pomConfig = {
    licenses {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
        }
    }
    developers {
        developer {
            id "kartetaro"
            name "Taro Karte"
            email "dev.share@plaid.co.jp"
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact sourceJar
            groupId project.group
            artifactId moduleName
            version version_name

            pom.withXml {
                def root = asNode()
                root.appendNode('description', "${moduleName} module for KARTE SDK")
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    pkg {
        repo = 'maven'
        name = project.group + ':' + moduleName
        userOrg = 'plaidev'
        licenses = ['Apache-2.0']
        vcsUrl = gitUrl + '.git'
        version {
            name = version_name
            released = new Date()
            vcsTag = version_name
            desc = 'https://developers.karte.io/docs/release-note-android-sdk'
        }
    }
    publications = ['maven']
}