// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.3.50'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // for upload maven repo
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'

        // for apply karte plugin to example project
        classpath 'io.karte.android:karte-gradle-plugin'
    }
}

plugins {
    id 'org.jetbrains.dokka' version '0.10.1'
    id 'com.jfrog.bintray' version '1.8.5'
    id 'maven-publish'
    id 'io.codearte.nexus-staging' version '0.22.0'
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}
configure(subprojects.findAll { !it.name.startsWith("sample_") }) {
    println "configure libraries: ${project.name}"

    apply from: '../buildscripts/projectDokka.gradle'
    task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
        outputFormat = 'javadoc'
        outputDirectory = "$buildDir/javadoc"
    }

    task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
        classifier = 'javadoc'
        from "$buildDir/javadoc"
    }

    apply from: '../buildscripts/projectJacoco.gradle'

    apply from: '../buildscripts/projectMaven.gradle'
    apply from: '../buildscripts/projectMavenAndroid.gradle'
    apply from: '../buildscripts/projectBintray.gradle'
    apply from: '../buildscripts/projectSonatype.gradle'
    publishMavenPublicationToMavenLocal.dependsOn "assembleRelease"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

apply from: 'buildscripts/ktlint.gradle'
apply from: 'buildscripts/dokka.gradle'
apply from: 'buildscripts/jacoco.gradle'

nexusStaging {
    packageGroup ='io.karte.android'
    username = ossrhUsername
    password = ossrhPassword
}
