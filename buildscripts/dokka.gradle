import org.apache.tools.ant.filters.ReplaceTokens

task dokkaModuleMd(type: Copy, group: 'documentation') {
    from 'buildscripts/dokka/Module.md'
    into 'build/dokka'
    filter(ReplaceTokens, tokens: [README: file("$rootDir/README.md").readLines('UTF-8').drop(2).join("\n")])
    filteringCharset = 'UTF-8'
}

dokkaHtmlMultiModule {
    def dir = "$rootDir/buildscripts/dokka"
    def configJson = """
        {
            "customStyleSheets":["${file("$dir/logo-styles.css")}","${file("$dir/custom-style-to-add.css")}"],
            "customAssets":["${file("$dir/logo-icon.svg")}"]
        }""".stripIndent().trim()
    pluginsMapConfiguration.set(["org.jetbrains.dokka.base.DokkaBase": configJson])
}

task publishDocs(type: Exec, group: 'documentation') {
    dependsOn dokkaHtmlMultiModule
    def branch = findProperty('branch')
    def args = ["ruby", "scripts/publish_docs.rb"]
    if (branch != null) {
        args.addAll(["--branch", branch])
    }
    commandLine args
    standardOutput = new ByteArrayOutputStream()
    ext.output = {
        return standardOutput.toString()
    }
    doLast {
        println(standardOutput.toString())
    }
}
