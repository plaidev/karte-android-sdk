apply plugin: 'org.jetbrains.dokka'

task javadocJar(type: Jar, dependsOn: 'dokkaJavadoc', group: 'documentation') {
    archiveClassifier.set("javadoc")
    from "$buildDir/dokka/javadoc"
}

afterEvaluate {
    dokkaHtml {
//        failOnWarning.set(true)
        dokkaSourceSets {
            named("main") {
                includeNonPublic.set(false)
                skipDeprecated.set(false)
                reportUndocumented.set(true)
                skipEmptyPackages.set(true)
                includes.from("$rootDir/build/dokka/Module.md")
                externalDocumentationLink {
                    url.set(new URL("https://plaidev.github.io/karte-sdk-docs/android/core/latest/core/"))
                    packageListUrl.set(new URL("https://plaidev.github.io/karte-sdk-docs/android/core/latest/core/package-list"))
                }
            }
        }
        def dir = "$rootDir/buildscripts/dokka"
        def configJson = """
            {
                "customStyleSheets":["${file("$dir/logo-styles.css")}","${file("$dir/custom-style-to-add.css")}"],
                "customAssets":["${file("$dir/logo-icon.svg")}"]
            }""".stripIndent().trim()
        pluginsMapConfiguration.set(["org.jetbrains.dokka.base.DokkaBase": configJson])
    }
    dokkaHtml.dependsOn rootProject.dokkaModuleMd
    rootProject.publishDocs.dependsOn dokkaHtml
}
